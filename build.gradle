// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta5'
        // This does not break the build when Android Studio is missing the JRebel for Android plugin.
        classpath 'com.zeroturnaround.jrebel.android:jr-android-gradle:1.0.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}



// Manual stopappEngine task and removeOldInstalls.
// They will be visible close to the compound task
// and save time when an error occurs with the app engine
// continuing to run in background (JNI timeout for test installation
// is possible problem).
task stopappEngine(dependsOn: ":backend:appengineStop") {}
task removeOldInstalls(dependsOn: ":app:uninstallAll") {}

// Moved evaluationDependsOn outside of the tasks
evaluationDependsOn ":app"
evaluationDependsOn ":backend"

project(":backend") {
    appengine {
        daemon true
    }
}

task runAppEngine(dependsOn: ":backend:appengineRun") {
    description 'Starts App Engine local server in daemon. Needs to be done first.'

    doLast {
        println "App engine is on!"
    }
}

task runTests(dependsOn: ":app:check") {
    description 'Runs all tests for the app. This includes connectedAndroidTests.'
    doLast {
        println "All tests have passed!"
    }
}

task runAppEngineTestAndStop(dependsOn: ":backend:appengineStop") {
    description 'Compound task. Runs app engine, makes the tests and stops the app engine.'
    doLast {
        println "Jobs done and engine stopped!"
    }
}
// Enforcing the tasks order.
runAppEngineTestAndStop.dependsOn runTests
runAppEngineTestAndStop.dependsOn runAppEngine
runTests.mustRunAfter runAppEngine
