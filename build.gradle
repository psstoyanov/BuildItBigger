// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta5'
        // This does not break the build when Android Studio is missing the JRebel for Android plugin.
        classpath 'com.zeroturnaround.jrebel.android:jr-android-gradle:1.0.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}




task stopappEngine(dependsOn: ":backend:appengineStop") {}
task removeOldInstalls(dependsOn: ":app:uninstallAll") {}


task runAppEngine(dependsOn: ":backend:appengineRun") {
    evaluationDependsOn ":backend"

    project(":backend") {
        appengine {
            daemon true
        }
    }

    doLast {
        println "App engine is on!"
    }
}

task runTests(dependsOn: ":app:connectedAndroidTest") {
    evaluationDependsOn ":app"
    doLast {
        println "All tests done"
    }
}

task runAppEngineTestAndStop(dependsOn: ":backend:appengineStop") {
    doLast {
        println "Jobs done and engine stopped"
    }
}
runAppEngineTestAndStop.dependsOn runTests
runAppEngineTestAndStop.dependsOn runAppEngine
runTests.mustRunAfter runAppEngine
